apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

//fixed data
def pkgName = 'sharedpreferences_livedata'
def libGroupId = "com.stefanosiano.powerfullibraries"                                           // Maven Group ID for the artifact
def libArtifactId = "sharedpreferences_livedata"                                                // Maven Artifact ID for the artifact
def libName = "Powerful SharedPreferences Live Data"                                            // Library name
def siteUrl = 'https://github.com/stefanosiano/PowerfulSharedPreferences'                       // Homepage URL of the library
def gitUrl = 'https://github.com/stefanosiano/PowerfulSharedPreferences.git'                    // Git repository URL
def libDescription = 'Adds Live Data support to PowerfulSharedPreferences'                      // Library description

//data to change on updates
def libVersion = "0.1.15"// This is the library version used when deploying the artifact
def gitTag = '0.1.15'

group = libGroupId //bintray org/group name
version = libVersion //version

android {
    compileSdkVersion 28
    android.buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName libVersion
        consumerProguardFiles 'psp-proguard-rules.txt'
    }
}

repositories {
    maven { url  "https://dl.bintray.com/stefanosiano/maven-repo" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0' //ViewModel and LiveData
    implementation 'com.stefanosiano.powerfullibraries:sharedpreferences:+'  //PowerfulSharedPreferences
}


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId libGroupId
                artifactId libArtifactId

                // Add your description here
                name libName
                description libDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'stefanosiano'
                        name 'Stefano Siano'
                        email 'stefano.siano91@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}


task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    excludes = ['**/*.kt'] // < ---- Exclude all kotlin files from javadoc file.
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavaDocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavaDocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavaDocsJar
    archives generateSourcesJar
}




// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']

    // Package info for BinTray
    pkg {
        repo = 'maven-repo'
        // it is the name that appears in bintray when logged
        name = pkgName
        desc = libDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        publish = true
        publicDownloadNumbers = true
        version {
            name = libVersion
            desc = libDescription
            released = new Date()
            vcsTag = gitTag
        }
    }
}